dnl
dnl Configure for SDPA 7.4.4
dnl

AC_INIT(sdpa, 7.4.4)
AM_INIT_AUTOMAKE
AC_PROG_RANLIB
AC_PROG_CC
AC_PROG_CXX
AC_CANONICAL_HOST

AC_ARG_WITH(blas,
[  --with-blas             Gives BLAS name you use(e.g., "-L/usr/lib -lblas")],
BLAS_LIBS=$withval
)

AC_ARG_WITH(lapack,
[  --with-lapack           Gives LAPACK name you use(e.g., "-L/usr/lib -llapack")],
LAPACK_LIBS=$withval
)

MUMPS_DIR=`pwd`/mumps/build

AC_SUBST(MUMPS_DIR)

dnl spooles requires -lm when linking.
LIB="-lm"

dnl How thread we link thread library esp. needed for spooles.
case "$host" in
        *-*-linux*|*-*-uclinux*)
            pthread_cflags="-D_REENTRANT"
            pthread_lib="-lpthread"
            ;;
        *-*-freebsd*|*-*-dragonfly*)
            pthread_cflags=""
            pthread_lib="-pthread"
            ;;
esac
LIBS="$LIBS $pthread_lib"
CXXFLAGS="$CXXFLAGS $pthread_cflags"
AC_SUBST(pthread_lib)

CFLAGS="$CFLAGS"
CXXFLAGS="$CXXFLAGS"
FFLAGS="$FFLAGS"
		 
dnl -fPIC is necessary to link with mex files on 64bit linux
case "$host" in
     x86_64-*-linux*)
        CFLAGS="$CFLAGS -fPIC"
        CXXFLAGS="$CXXFLAGS -fPIC"
        FFLAGS="$FFLAGS -fPIC"
        ;;
esac

AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)

AC_PROG_F77
AC_F77_LIBRARY_LDFLAGS
AC_F77_WRAPPERS
AC_SUBST(FLIBS)

dnl check BLAS/LAPACK rountines
HAVE_BLAS=""
dnl default blas name is libblas, thus we pass -lblas.
if test x"$BLAS_LIBS" = x; then
  BLAS_LIBS="-lblas"
fi
save_LIBS="$LIBS"
AC_F77_FUNC(dgemm)
LIBS="$LIBS $BLAS_LIBS $FLIBS"
AC_CHECK_FUNC($dgemm, [HAVE_BLAS=yes])
LIBS="$save_LIBS"
AC_SUBST(BLAS_LIBS)

if test x"$HAVE_BLAS" != xyes; then
  echo "[1] Please install one of your favorate BLAS"
  echo "BLAS:      http://www.netlib.org/blas/"
  echo "ATLAS:     http://math-atlas.sourceforge.net/"
  echo "GotoBLAS:  http://www.tacc.utexas.edu/resources/software/"
  echo "[2] Please verify config.log to check libraries are linked correctrly"
  AC_MSG_ERROR([No BLAS found])
fi
          
dnl Check whether we can call LAPACK subroutines
HAVE_LAPACK=""
dnl default lapack name is liblapack, thus we pass -llapack.
if test x"$LAPACK_LIBS" = x; then
  LAPACK_LIBS="-llapack"
fi
save_LIBS="$LIBS"
AC_F77_FUNC(dsyev)
LIBS="$LIBS $LAPACK_LIBS $BLAS_LIBS $FLIBS"
AC_CHECK_FUNC($dsyev, [HAVE_LAPACK=yes])
LIBS="$save_LIBS"
AC_SUBST(LAPACK_LIBS)

if test x"$HAVE_LAPACK" != xyes; then
  echo "Please install one of your favorate LAPACK"
  echo "LAPACK:   http://www.netlib.org/lapack/"
  AC_MSG_ERROR([No LAPACK found])
fi

AC_OUTPUT(Makefile)
